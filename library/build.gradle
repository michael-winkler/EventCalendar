plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'maven-publish'
}

android {
    namespace 'com.nmd.eventCalendar'
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 35
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig = true
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain(17)
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = "com.nmd"
                artifactId = "eventCalendar"
                version = project.property("VERSION_NAME") as String

                from components.release

                pom {
                    name.set(artifactId)
                    description.set("An Android event calendar library written in Kotlin, offering customizable views and event handling, supporting calendar week display and edge-to-edge functionality.")
                    url.set("https://github.com/michael-winkler/EventCalendar")

                    licenses {
                        license {
                            name.set("Apache License 2.0")
                            url.set("https://www.apache.org/licenses/LICENSE-2.0")
                        }
                    }

                    developers {
                        developer {
                            id.set("michael-winkler")
                            name.set("Michael Winkler")
                        }
                    }

                    scm {
                        connection.set("scm:git:git://github.com/michael-winkler/EventCalendar.git")
                        developerConnection.set("scm:git:ssh://github.com/michael-winkler/EventCalendar.git")
                        url.set("https://github.com/michael-winkler/EventCalendar")
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Android X
    implementation 'androidx.activity:activity-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation('androidx.fragment:fragment-ktx:1.8.6')
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'

    // Gson
    implementation('com.google.code.gson:gson:2.12.1')

    // Google Material Design
    implementation 'com.google.android.material:material:1.13.0-alpha11'
}